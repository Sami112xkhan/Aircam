plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def keystorePropertiesFile = rootProject.file("signing.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace 'com.github.digitallyrefined.androidipcamera'
    compileSdk 35

    defaultConfig {
        applicationId "com.github.digitallyrefined.androidipcamera"
        minSdk 24
        targetSdk 35
        versionCode 1
        versionName "0.0.10"
    }
    
    // 16KB page size workaround: extract native libs at install time
    // This bypasses alignment issues with CameraX native libraries
    packaging {
        jniLibs {
            useLegacyPackaging = true
        }
    }

    signingConfigs {
        release {
          if (keystorePropertiesFile.exists() && keystoreProperties['storeFile']) {
              storeFile file(keystoreProperties['storeFile'])
              storePassword keystoreProperties['storePassword']
              keyAlias keystoreProperties['keyAlias']
              keyPassword keystoreProperties['keyPassword']
          } else {
            def isReleaseBuild = gradle.startParameter.taskNames.any { it.contains('Release') }
            def errorMessage = 'Signing configuration is not set up. Please copy signing.properties.default to signing.properties and fill in the values and create the keystore file via Android Studio - Generate Signed App Bundle/APK.'
            if (isReleaseBuild) {
              throw new GradleException(errorMessage)
            } else {
              logger.warn(errorMessage)
            }
          }
      }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            splits {
                abi {
                    enable true
                    reset()
                    include "armeabi-v7a", "arm64-v8a"
                    universalApk true
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def versionName = variant.versionName
            def abi = output.getFilter(com.android.build.OutputFile.ABI) ?: "universal"
            def buildType = variant.buildType.name

            def newApkName = "${project.rootProject.name}-${versionName}-${abi}-${buildType}.apk"
            outputFileName = newApkName
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation "androidx.preference:preference-ktx:1.2.1"

    // Security dependencies
    implementation "androidx.security:security-crypto:1.1.0-alpha06"

    // CameraX dependencies (1.4.1+ has 16KB page size alignment)
    implementation 'androidx.camera:camera-core:1.4.1'
    implementation 'androidx.camera:camera-camera2:1.4.1'
    implementation 'androidx.camera:camera-lifecycle:1.4.1'
    implementation 'androidx.camera:camera-view:1.4.1'

    // Coroutines for asynchronous operations
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'

    // BouncyCastle for certificate generation
    implementation 'org.bouncycastle:bcprov-jdk15on:1.70'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.70'
}

